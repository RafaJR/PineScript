//@version=5
strategy(title="Alligator",
         shorttitle="Estrategia basada en el indicador Alligator de Bill Williams", overlay = true, pyramiding = 0, 
         calc_on_order_fills = false, commission_type =  strategy.commission.percent, 
         commission_value = 0.1, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, 
         initial_capital=100, currency=currency.EUR)
stopLossPercent = input(6.5, title="Porcentaje del Stop Loss (%)")

lengthJaw = input.int(13, title="Jaw Length")
offsetJaw = input.int(8, title="Jaw Offset")
lengthTeeth = input.int(8, title="Teeth Length")
offsetTeeth = input.int(5, title="Teeth Offset")
lengthLips = input.int(5, title="Lips Length")
offsetLips = input.int(3, title="Lips Offset")

// Calcular las medias móviles suavizadas
lips = ta.sma(ta.sma(hl2, lengthLips), lengthLips)
teeth = ta.sma(ta.sma(hl2, lengthTeeth), lengthTeeth)
jaw = ta.sma(ta.sma(hl2, lengthJaw), lengthJaw)

// Pintar las medias móviles con los offsets correspondientes
plot(lips[offsetLips], title="Lips", color=color.green, linewidth=2)
plot(teeth[offsetTeeth], title="Teeth", color=color.red, linewidth=2)
plot(jaw[offsetJaw], title="Jaw", color=color.blue, linewidth=2)

// Variables para la detecciónde señales de compra/venta o la parada de las mismas.
var bool longEntry = false
var bool shortEntry = false
var bool closeLongEntry = false
var bool closeShortEntry = false

// Indicadores de operaciones abiertas.
var bool longOperationOn = false
var bool shortOperationOn = false

// Variables para establecer nomenclatura de operaciones
var int longOpCount = 0
var int shortOpCount = 0
var string longOpName = ""
var string shortOpName = ""

// Se considera momento favorable para la compra cuando se produce un cruce que ordena las medias según orden apropiado alcista.
longEntry := not longOperationOn and ta.crossunder(jaw, teeth) and lips > teeth and teeth > jaw
// Se cierra una operación de compra cuando se produce un cruce que ordena las medias según orden apropiado bajista
closeLongEntry := longOperationOn and ta.crossover(jaw, teeth) and (lips < teeth and teeth < jaw)
// Se considera momento favorable para la venta cuando se produce un cruce que ordena las medias en orden apropiado bajista
shortEntry := not shortOperationOn and ta.crossover(jaw, teeth) and (lips < teeth and teeth < jaw)
// Se cierra una operación de venta cuando se produce un cruce que ordena las medias en orden apropiado alcista
closeShortEntry := shortOperationOn and ta.crossunder(jaw, teeth) and lips > teeth and teeth > jaw

// Imprimir una barra vertical verde cuando `longEntry` es verdadero
if (longEntry)
    longOperationOn := true
    shortOperationOn := false
    longOpCount := longOpCount + 1 
    longOpName := "Long" + str.tostring(longOpCount)
    label.new(x=bar_index, y=low, text=longOpName, style=label.style_label_up, color=#1fdf26, textcolor=#000000, size=size.small, textalign=text.align_center)
    stopLossPrize = close * (1 - stopLossPercent / 100)
    strategy.entry(longOpName, strategy.long)
    strategy.exit(longOpName + "SL", longOpName, stop = stopLossPrize)
if(closeLongEntry)
    longOperationOn := false
    label.new(x=bar_index, y=low, text="Close"+longOpName, style=label.style_xcross, color=#1fdf26, textcolor=#ffffff, size=size.small, textalign=text.align_center)
    strategy.close(longOpName) 
if(shortEntry)
    shortOperationOn := true
    longOperationOn := false
    shortOpCount := shortOpCount + 1 
    shortOpName := "Short" + str.tostring(shortOpCount)
    label.new(x=bar_index, y=high, text=shortOpName, style=label.style_label_down, color=#e90808, textcolor=#000000, size=size.small, textalign=text.align_center)
    stopLossPrize = close * (1 + stopLossPercent / 100)
    strategy.entry(shortOpName, strategy.short)
    strategy.exit(shortOpName + "SL", shortOpName, stop = stopLossPrize)
if(closeShortEntry)
    shortOperationOn := false
    label.new(x=bar_index, y=high, text="Close"+shortOpName, style=label.style_xcross, color=#e90808, textcolor=#ffffff, size=size.small, textalign=text.align_center)
    strategy.close(shortOpName)
